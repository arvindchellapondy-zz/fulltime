apply plugin: 'com.android.application'

// A string that holds a build name.
def devBuildName = "debug"
// Used to get to the source of the project.
def profileRootSource = projectDir.toString() + File.separator + "src"
// Used as part of file name.
def devRootSource = profileRootSource + File.separator + devBuildName
// Branch name that the build is created from.
def gitBranchName = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
// This contains the time when the build was generated.
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
// This contains only the date.
def buildDate = buildTime.split('T')[0].replace('-', " ");

def debugProperties = new Properties()
debugProperties.load(new FileInputStream(rootProject.file("debug.properties")))
android.signingConfigs.debug.storeFile rootProject.file(debugProperties.keyStore)
android.signingConfigs.debug.storePassword debugProperties.keyStorePassword
android.signingConfigs.debug.keyAlias debugProperties.keyAlias
android.signingConfigs.debug.keyPassword debugProperties.keyAliasPassword

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "zerobase.us.fulltime"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        debug {
            storeFile file(devRootSource
                    + File.separator
                    + "debug.keystore")
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix devBuildName.toUpperCase().concat("-${gitBranchName}-".toUpperCase()).concat("${buildDate}")
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(
                            output.outputFile.parent,
                            output.outputFile.name.replace("app-debug.apk", "${versionNameSuffix}.apk"))
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okio:okio:1.6.0'
    compile 'com.squareup.retrofit.samples:parent:1.9.0'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.14'
}
